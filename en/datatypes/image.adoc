= Image! datatype
:toc:
:numbered:

== Abstract

`Image!` values contain a series! of RGBA values, which represent pixels in a 2D image.

`Image!` is a member of the following typesets: `any-type!`, `default!`, `series!`

== Creation

Image! values can be created using literal syntax, or at runtime by using a `make` constructor or `to` conversion.

== Literal syntax

```
<image> ::= make image! <image-spec>
<image-spec> ::= <pair> | [<pair> <tuple> <binary>°] | [<pair> <binary> <binary>°]
```

=== Runtime creation

When <image-spec> is pair! or [pair!], the pair defines the image size, and RGB and Alpha values are set to 255. 

```red
>> make image! 2x3
== make image! [2x3 #{FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF}]
```

When <image-spec> is [pair! tuple!], the pair defines the image size, and the tuple defines the initial RGB color and opacity of all pixels. 

With RGB. Opacity defaults to fully opaque (255):

```red
>> img: make image! [2x3 255.210.0]
== make image! [2x3 #{FFD200FFD200FFD200FFD200FFD200FFD200}]
```

With RGB and Alpha channel (RGBA):

```red
>> img: make image! [2x3 255.210.0.120]
== make image! [2x3 #{FFD200FFD200FFD200FFD200FFD200FFD200} #{
787878787878}]
```

// When <image-spec> is [pair! tuple! binary!], the pair defines the image size, the tuple defines the initial RGB color of all pixels, and the binary value defines the intial opacity of the image.


// When <image-spec> is [pair! binary! binary!], the pair defines the image size, the first binary value defines the initial RGB color of all pixels, and the second binary value defines the intial opacity of the image.

== Path accessors (Image components)

=== /alpha

Gets or sets the Alpha channel value of an image.

```red
>> img/alpha
== #{787878787878}
```

```red
>> img/alpha: 42
== 42

>> img/alpha
== #{2A2A2A2A2A2A}
```

=== /argb

Gets or sets the Alpha and RGB values of an image.

```red
>> img/argb
== #{00D2FF8700D2FF8700D2FF8700D2FF8700D2FF8700D2FF87}
```

```red
>> img/argb: 42
== 42

>> img/argb
== #{00002AFF00002AFF00002AFF00002AFF00002AFF00002AFF}
```

=== /rgb

Gets or sets the RGB values of an image.

```red
>> img/rgb
== #{FFD200FFD200FFD200FFD200FFD200FFD200}
```

```red
>> img/rgb: 42
== 42

>> img/rgb
== #{2A00002A00002A00002A00002A00002A0000}
```
=== /size

 Returns the size of an image as a pair value.

```red
>> img/size
== 2x3
```


== Conversions

`To image!` converts Red/View face! objects to image values.

```red
>> lay: layout [button "Hi there!"]
== make object! [
    type: 'window
    offset: none
    size: 83x45
    text: none
    image: none
    color: none
    menu: none
    data:...

>> view/no-wait lay
== make object! [
    type: 'window
    offset: 644x386
    size: 126x45
    text: "Red: untitled"
    image: none
    color: none
    menu:...

    >> to image! lay
== make image! [132x73 #{
    0000000000000000000000004D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D
    4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4...
```

== Testing values

Use `image?` to check if a value is of the `image!` datatype.

```red
>> image? img
== true
```

Use `type?` to return the datatype of a given value.

```red
>> type? img
== image!
```


== Pre-defined words

=== Functions

`draw`, `image?`, `layout`, `series?`, `to-image`
