= Image! datatype
:toc:
:numbered:

== Abstract

`Image!` values contain a series! of RGBA values, which represent pixels in a 2D image.

`Image!` is a member of the following typesets: `any-type!`, `default!`, `series!`

== Creation

Image! values can be created using literal syntax, or at runtime by using a `make` constructor or `to` conversion.

== Literal syntax

```
<image> ::= make image! <image-spec>
<image-spec> ::= <pair> | [<pair> <tuple> <binary>°] | [<pair> <binary> <binary>°]
```

=== Runtime creation

When <image-spec> is pair! or [pair!], the pair defines the image size, and RGB and Alpha values are set to 255. 

```red
>> make image! 1x1
== make image! [1x1 #{FFFFFF}]
```

```red
>> make image! [1x1]
== make image! [1x1 #{FFFFFF}]
```

When <image-spec> is [pair! tuple!], the pair defines the image size, and the tuple defines the initial RGB color and opacity of all pixels. 

With RGB. Opacity defaults to fully opaque (255):

```red
>> img: make image! [1x1 255.210.0]
== make image! [1x1 #{FFD200}]
```

With RGB and Alpha channel (RGBA):

```red
>> img: make image! [1x1 255.210.0.42]
== make image! [1x1 #{FFD200} #{
2A}]
```

When <image-spec> is [pair! tuple! binary!], the pair defines the image size, the tuple defines the initial RGB color of all pixels, and the binary value defines the intial opacity of the image.

```red
>> img: make image! [1x1 255.210.0 #{42}]
== make image! [1x1 #{FFD200}]
```

When <image-spec> is [pair! binary! binary!], the pair defines the image size, the first binary value defines the initial RGB color of all pixels, and the second binary value defines the intial opacity of the image.

```red
>> img: make image! [1x1 #{FFD200} #{2A}]
== make image! [1x1 #{FFD200} #{
2A}]
```

[NOTE, caption=Note]

When using binary! values, you must repeat the binary value to match the image size. For any image size of X by Y, multiply X by Y to determine the number of times that the binary value must be repeated.

*Examples*

```red
>> make image! [2x2 #{FFD200FFD200FFD200FFD200} #{2A2A2A2A}]
== make image! [2x2 #{FFD200FFD200FFD200FFD200} #{
2A2A2A2A}]                                        ; RGB and A values in binary repeated 2 * 2 times
```
Binary values are padded with 00 if binary values are not repeated:

```red
>> make image! [2x2 #{FFD200} #{2A}]
== make image! [2x2 #{FFD200000000000000000000} #{
2A000000}]
```

```red
>> make image! [2x2 255.210.0.42]                  
== make image! [2x2 #{FFD200FFD200FFD200FFD200} #{ ; RGB and A values in binary repeated 2 * 2 times
2A2A2A2A}]

>> make image! [2x3 255.210.0.42]
== make image! [2x3 #{FFD200FFD200FFD200FFD200FFD200FFD200} #{
2A2A2A2A2A2A}]                                     ; RGB and A values in binary repeated 2 * 3 times
```

== Path accessors (Image components)

=== /alpha

Gets or sets the Alpha channel value of an image.

*Examples*

```red
>> img/alpha
== #{2A}
```

```red
>> img/alpha: 210
== 210

>> img/alpha
== #{D2}
```

=== /argb

Gets or sets the Alpha and RGB values of an image.

*Examples*

```red
>> img: make image! [1x1 255.210.0.120]
== make image! [1x1 #{FFD200} #{
78}]

>> img/argb
== #{00D2FF87}
```

```red
>> img/argb: 255.255.210.0
== 255.255.210.0

>> img/argb
== #{D2FFFFFF}
```

=== /rgb

Gets or sets the RGB values of an image.

*Examples*

```red
>> img: make image! [1x1 255.210.0.120]
== make image! [1x1 #{FFD200} #{
78}]

>> img/rgb
== #{FFD200}
```

```red
>> img/rgb: 255.255.255
== 255.255.255

>> img/rgb
== #{FFFFFF}
```
=== /size

 Returns the size of an image as a pair value.

*Examples*

```red
>> img/size
== 1x1
```


== Conversions

`To image!` converts Red/View face! objects to image values.

```red
>> lay: layout [button "Hi there!"]
== make object! [
    type: 'window
    offset: none
    size: 83x45
    text: none
    image: none
    color: none
    menu: none
    data:...

>> view/no-wait lay
== make object! [
    type: 'window
    offset: 644x386
    size: 126x45
    text: "Red: untitled"
    image: none
    color: none
    menu:...

    >> to image! lay
== make image! [132x73 #{
    0000000000000000000000004D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D
    4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4D4...
```

== Testing values

Use `image?` to check if a value is of the `image!` datatype.

```red
>> image? img
== true
```

Use `type?` to return the datatype of a given value.

```red
>> type? img
== image!
```


== Pre-defined words

=== Functions

`draw`, `image?`, `layout`, `series?`, `to-image`
